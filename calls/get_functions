#!/usr/bin/env perl

use common::sense;

use File::Slurp qw(read_file);

my $ret  = qr'';
my $name = qr'(?<name>[a-zA-Z]\w*)';
my $args = qr'(?>(?<args>\((?:(?>[^\(\)]+)|(?&args))*\)))';
my $body = qr'(?>(?<fbody>\{(?:(?>[^\{\}]+)|(?&fbody))*\}))';

my $uniq = 1;
my $sort = 1;

sub remove_strings {
   $_[0] =~ s/(?<!\\|')"(?:[^\\"]+|\\.)*"/""/mg;
}

sub remove_comments
{
   $_[0] =~ s#/\*[^*]*\*+([^/*][^*]*\*+)*/|//([^\\]|[^\n][\n]?)*?\n|("(\\.|[^"\\])*"|'(\\.|[^'\\])*'|.[^/"'\\]*)#defined $3 ? $3 : ""#gse;
}

my @names;

foreach (@ARGV) {
   my $file = read_file($_);
   remove_comments($file);
   remove_strings($file);
   while ( $file =~ m/$ret\s*+$name\s*+$args\s*+$body/gmx ) {
      push @names, $+{name};
   }
}

@names = sort @names if $sort;

if ($uniq) {
   my %check;
   @names = grep { !$check{$_}++ } @names;
}

$"="\n";
say "@names";

