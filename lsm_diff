#!/usr/bin/env perl

use strict;
use warnings;

use feature qw(say);
use re '/aa';

use utf8::all;
use IO::Interactive;
use File::Spec::Functions qw(catfile);
use File::Slurp qw(read_file write_file);
use Getopt::Long qw(:config gnu_getopt);
use Text::ANSITable;
use Class::CSV;

use lib::abs 'lib';

use Local::C::Transformation;
use Local::String::Utils qw(normalize);
use Local::Kernel::Common qw(check_kernel_files_readable autodetect_kernel_directory);


use constant SECURITY_H => catfile(qw(include linux security.h));
my $sec_hooks_file = 'sec-hooks.c';

my $kdir;
my $mdir = '';

my $output = '';
my $csvout = 0;

GetOptions(
   'kdir|k=s'      => sub { if (check_kernel_files_readable($_[1], SECURITY_H)) { $kdir = $_[1] } else { die(qq(Incorrect path to kernel: "$_[1]"\n)) } },
   'module|m=s'    => \$mdir,
   'file|f=s'      => \$sec_hooks_file,
   'output|o=s'    => \$output,
   'csv-output|c!' => \$csvout,
) or die "Incorrect usage!\n";

die(qq(Incorrect path to module: "$mdir"\n)) if $mdir && !( -d $mdir && -r catfile($mdir, $sec_hooks_file) );

$kdir //= autodetect_kernel_directory(files => [SECURITY_H]);

die "Can't find path to linux kernel sources directory.\n"
   unless $kdir;

unless ($mdir) {
   if ( -r $sec_hooks_file) {
      $mdir = '.';
   } elsif ( defined $ENV{CURRENT_PROJECT} && -r catfile($ENV{CURRENT_PROJECT}, $sec_hooks_file) ) {
      $mdir = $ENV{CURRENT_PROJECT};
   }
}

die "Can't find path to module sources directory.\n"
   unless $mdir;

my $kernel_version = do {
   open my $MK, '<', "$kdir/Makefile";
   join('.', map { <$MK> =~ m/=\s*(\d+)/ } (1 .. 3))
};


my $sec_file   = read_file(catfile($kdir, SECURITY_H));
my $hooks_file = read_file(catfile($mdir, $sec_hooks_file));

adapt($sec_file,   comments => 1);
adapt($hooks_file, comments => 1);

sub argname {
   $_[0] =~ m/(\w+)$/;
   return $1 ne 'void' ? $1 : '';
}

sub argtype {
   $_[0] =~ m/^(.*?)(?=\s*\w+$)/;
   return $1 eq '' ? 'void' : $1;
}

sub parse_structure_defition_ops_fields {
   my $conf = $_[1];
   map { normalize($_); if (m/\.(\w+)\s*=\s*(\w+)/) {$1 => +{ init => $2, conf => $conf }} else { () } } split /,/, $_[0];
}

sub parse_structure_declaration_ops_fields {
   my $conf = $_[1];
   my @fields = map normalize($_), split(/;/, $_[0]);

   # filter non-functions
   @fields = grep { m/\(\*\w+\)\s*\(/ } @fields;

   map { m/\(\*(\w+)\)\s*\((.*?)\)$/; $1 => +{ args => $2, conf => $conf } } @fields;
}

sub parse_security_operations_structure {
   my $handler = $_[1];
   my %structure;

   if ($_[0] =~ m/struct\s+security_operations(?:\s+\w+\s*=)?\s*(?<br>\{(?:(?>[^\{\}]+)|(?&br))+\})/m) {
      my @lines = split /\n/, $1;
      my $buf = '';
      my @ifdefs;
      my %preprocessed;

      foreach (@lines) {
         next if $_ =~ m/^\s*\n$/;
         if ($_ =~ m/^\s*\#\s*if\s+(?<cond>.+)$/) {
            %structure = (%structure, $handler->($buf, join( ' && ', @ifdefs)));
            $buf = '';
            push @ifdefs, normalize($+{cond});
            next;
         }
         if ($_ =~ m/^\s*\#\s*ifdef\s+(?<cond>\w+)\s*$/) {
            %structure = (%structure, $handler->($buf, join( ' && ', @ifdefs)));
            $buf = '';
            push @ifdefs, normalize($+{cond});
            next;
         }
         if ($_ =~ m/^\s*\#\s*ifndef\s+(?<cond>\w+)\s*$/) {
            %structure = (%structure, $handler->($buf, join( ' && ', @ifdefs)));
            $buf = '';
            push @ifdefs, '! ' . normalize($+{cond});
            next;
         }
         if ($_ =~ m/^\s*\#\s*else/) {
            %structure = (%structure, $handler->($buf, join( ' && ', @ifdefs)));
            $buf = '';
            push @ifdefs, '! ' . pop(@ifdefs);
            next;
         }
         if ($_ =~ m/^\s*\#\s*endif/) {
            %structure = (%structure, $handler->($buf, join( ' && ', @ifdefs)));
            $buf = '';
            pop @ifdefs;
            next;
         }
         $buf .= $_ . "\n";
      }
      die("Preprocessing error @ifdefs.") if scalar @ifdefs ne 0;
   } else {
      die("Can't find security_operations definition/declaration.\n");
   }

   return %structure;
}

my %str_kernel = parse_security_operations_structure $sec_file, \&parse_structure_declaration_ops_fields;
my %str_module = parse_security_operations_structure $hooks_file, \&parse_structure_defition_ops_fields;


foreach (keys %str_kernel) {
   if (defined $str_module{$_}) {
      $str_kernel{$_}{module} = $str_module{$_};
      delete $str_module{$_};
   }
}


my $columns = ['Idx', 'Указатель на функцию', 'Функция', 'Аргументы', 'Конфигурация ядра', 'Конфигурация модуля'];
my $obj;
my $addfunc;
my $retfunc;

unless ($csvout) {
   my $t1 = Text::ANSITable->new;
   $t1->border_style('Default::bold');
   if ( IO::Interactive::is_interactive() && !$output ) {
      $t1->color_theme('Default::default_gradation');
   } else {
      $t1->color_theme('Default::no_color');
   }

   $t1->columns($columns);

   $t1->set_column_style('Idx',                  pad  => 0);
   $t1->set_column_style('Указатель на функцию', align  => 'left');
   $t1->set_column_style('Функция',              align  => 'middle');
   $t1->set_column_style('Аргументы',            align  => 'middle');
   $t1->set_column_style('Конфигурация ядра',    align  => 'middle');
   $t1->set_column_style('Конфигурация модуля',  align  => 'middle');

   #$t1->column_filter(sub { $_ ne'Аргументы' });
   $obj = \$t1;
   $addfunc = sub { Text::ANSITable::add_row($_[0], $_[1]); Text::ANSITable::add_row_separator($_[0], $_[1]); };
   $retfunc = sub { $_[0] . "\n" .  Text::ANSITable::draw($_[1]); };
} else {
   my $csv = Class::CSV->new(
      fields         => $columns,
      line_separator => "\r\n",
      csv_xs_options => {
         sep_char          => ';',
      },
   );

   $obj = \$csv;
   $addfunc = \&Class::CSV::add_line;
   $retfunc = sub { $_[0] . "\r\n" . join(';', map { '"' . $_ . '"' } @$columns) . "\r\n" . Class::CSV::string($_[1]); };
}


my $counter = 1;

foreach (reverse sort {defined $str_kernel{$a}{module} <=> defined $str_kernel{$b}{module}} keys %str_kernel) {
   my $init  = defined $str_kernel{$_}{module} ? $str_kernel{$_}{module}{init} : undef;
   my $args  = $str_kernel{$_}{args};
   my $kconf = $str_kernel{$_}{conf};
   my $mconf = defined $str_kernel{$_}{module} ? $str_kernel{$_}{module}{conf} : undef;
   my $line  = [$counter++, $_, $init, $args, $kconf, $mconf];
   $addfunc->($$obj, $line)
}


my $str = $retfunc->(qq("Таблица функций в security_operations (версия ядра $kernel_version)"), $$obj);
if ($output) {
   write_file($output, { binmode => ':utf8' }, $str);
} else {
   print $str;
}

if (keys %str_module) {
   $columns = ['Idx', 'Указатель на функцию', 'Функция', 'Конфигурация модуля'];

   #TODO: добавить аргументы
   unless ($csvout) {
      my $t2 = Text::ANSITable->new;
      $t2->border_style('Default::bold');
      if ( IO::Interactive::is_interactive() && !$output ) {
         $t2->color_theme('Default::default_gradation');
      } else {
         $t2->color_theme('Default::no_color');
      }


      $t2->columns($columns);

      $t2->set_column_style('Idx',                  pad  => 0);
      $t2->set_column_style('Указатель на функцию', align  => 'left');
      $t2->set_column_style('Функция',              align  => 'middle');
      $t2->set_column_style('Конфигурация модуля',  align  => 'middle');

      $obj = \$t2;
   } else {
      my $csv = Class::CSV->new(
         fields         => $columns,
         line_separator => "\r\n",
         csv_xs_options => {
            sep_char          => ';',
         },
      );

      $obj = \$csv;
   }


   $counter = 1;

   foreach (keys %str_module) {
      my $init  = $str_module{$_}{init};
      my $mconf = $str_module{$_}{conf};
      my $line = [$counter++, $_, $init, $mconf];

      $addfunc->($$obj, $line);
   }

   $str = "\r\n" . $retfunc->(qq("Таблица функций, отсутствующих в security_operations (версия ядра $kernel_version)"), $$obj);
   if ($output) {
      write_file($output, { binmode => ':utf8', append => 1 }, $str);
   } else {
      print "\r\n" . $str;
   }
}

